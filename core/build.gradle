/*
 * Created by Orchextra
 *
 * Copyright (C) 2016 Gigigo Mobile Services SL
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
  repositories {
    jcenter()
  }

  dependencies {
    classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.6.2'
  }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply from: '../config/jacoco.gradle'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'com.jfrog.bintray'

group = 'com.gigigo.orchextra'
version = '0.0.1'

ext {
  bintrayRepo = 'maven'
  bintrayName = 'com.gigigo.orchextra:core'

  publishedGroupId = 'com.gigigo.orchextra'
  libraryName = 'core'
  artifact = 'core'

  libraryDescription = 'Orchextra core'

  siteUrl = 'https://github.com/Orchextra/orchextra-android-sdk'
  gitUrl = 'https://github.com/Orchextra/orchextra-android-sdk.git'

  libraryVersion = '0.0.1'

  developerId = 'gigigo-desarrollo'
  developerName = 'Android Gigigo'
  developerEmail = 'gigigo.desarrollo@gmail.com'

  licenseName = 'The Apache Software License, Version 2.0'
  licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
  allLicenses = ["Apache-2.0"]
}

def STRING = "String"
def BOOLEAN = "boolean"

android {
  compileSdkVersion 25
  buildToolsVersion '26'

  defaultConfig {
    minSdkVersion 14
    targetSdkVersion 25
    versionCode 1
    versionName "3.0.0"

    buildConfigField STRING, "CORE_API_URL", '"https://core.s.orchextra.io/"'
    buildConfigField STRING, "TRIGGER_API_URL", '"https://tr.s.orchextra.io/"'
    buildConfigField BOOLEAN, "NETWORK_LOG", "true"

    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  buildTypes {

    debug {
      debuggable true
    }

    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  lintOptions {
    quiet true
    abortOnError false
    ignoreWarnings true
  }
}

dexcount {
  format = "list"
  includeClasses = false
  includeFieldCount = true
  includeTotalMethodCount = true
  orderByMethodCount = false
  verbose = true
  maxTreeDepth = Integer.MAX_VALUE
  enableForInstantRun = false
  runOnEachAssemble = false
}

dependencies {

  compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

  compile 'com.android.support:design:25.3.1'
  compile 'com.squareup.retrofit2:retrofit:2.3.0'
  compile 'com.squareup.retrofit2:converter-moshi:2.3.0'
  compile 'com.squareup.okhttp3:okhttp:3.4.2'
  compile 'com.squareup.okhttp3:logging-interceptor:3.4.2'
  compile 'com.google.android.gms:play-services-location:11.0.4'
  compile 'com.j256.ormlite:ormlite-android:5.0'
  compile 'com.google.android.gms:play-services-gcm:11.0.4'

  testCompile 'junit:junit:4.12'
  testCompile 'com.nhaarman:mockito-kotlin:1.5.0'
  testCompile 'org.amshove.kluent:kluent:1.26'

  androidTestCompile 'com.nhaarman:mockito-kotlin:1.5.0'
  androidTestCompile 'com.android.support.test:runner:0.5'
  androidTestCompile 'com.android.support.test:rules:0.5'
  androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
    exclude group: 'com.android.support', module: 'support-annotations'
  })
}

apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/installv1.gradle'
apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/bintrayv1.gradle'